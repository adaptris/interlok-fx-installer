buildscript {
    dependencies {
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
    }
    repositories {
        mavenCentral()

    }
}

import org.apache.tools.ant.filters.*

plugins {
    // Apply the java-library plugin to add support for Java Library
    id "application"
    id "java-library"
	id "jacoco"
}

apply plugin: "javafx-gradle-plugin"

ext {
  interlokCoreVersion = project.hasProperty("interlokCoreVersion") ? project.getProperty("interlokCoreVersion") : "3.10-SNAPSHOT"
  releaseVersion = project.hasProperty("releaseVersion") ? project.getProperty("releaseVersion") : "3.10-SNAPSHOT"
  nexusBaseUrl = project.hasProperty("nexusBaseUrl") ? project.getProperty("nexusBaseUrl") : "https://nexus.adaptris.net/nexus"
  mavenPublishUrl = project.hasProperty("mavenPublishUrl") ? project.getProperty("mavenPublishUrl") : nexusBaseUrl + "/content/repositories/snapshots"
  javadocsBaseUrl = nexusBaseUrl + "/content/sites/javadocs/com/adaptris"
  repoUsername = project.hasProperty("repoUsername") ? project.getProperty("repoUsername") : "unknown"
  repoPassword = project.hasProperty("repoPassword") ? project.getProperty("repoPassword") : "unknown"
  defaultNexusRepo = project.hasProperty("defaultNexusRepo") ? project.getProperty("defaultNexusRepo") : "https://repo1.maven.org/maven2/"
  offlineJavadocPackageDir = new File(project.buildDir, "offline-javadoc-packages")

  interlokJavadocs= project.hasProperty("interlokJavadocs") ? project.getProperty("interlokJavadocs") : javadocsBaseUrl + "/interlok-core/" + interlokCoreVersion
  interlokCommonJavadocs= project.hasProperty("interlokCommonJavadocs") ? project.getProperty("interlokCommonJavadocs") : javadocsBaseUrl + "/interlok-common/" + interlokCoreVersion
  componentName="Interlok Installer Fx"
  organizationName = "Adaptris Ltd"
  organizationUrl = "http://interlok.adaptris.net"
  slf4jVersion = "1.7.29"
}

repositories {
  mavenCentral()
  maven { url "$defaultNexusRepo" }
  maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url "$nexusBaseUrl/content/groups/private"
    }
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
  maven { url "https://repo.gradle.org/gradle/libs-releases-local/" }
}

dependencies {

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "com.google.guava:guava:23.0"
    
    implementation "org.gradle:gradle-tooling-api:6.0.1"

    // Use JUnit test framework
    testImplementation "junit:junit:4.12"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

processResources {
     filesMatching("**/*.properties") {
	    filter ReplaceTokens, tokens: [
	        "release.version": project.property("releaseVersion")
	    ]
     }
}

jar {
    manifest {
        attributes "Main-Class": "com.adaptris.fxinstaller.Launcher"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jfx {
    mainClass = "com.adaptris.fxinstaller.Launcher"
    vendor = "Adaptris"
}

mainClassName = "com.adaptris.fxinstaller.Launcher"

jacoco {
  toolVersion="0.8.3"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

test {
  include "com/adaptris/**/*Test*.class"
}

check.dependsOn jacocoTestReport
